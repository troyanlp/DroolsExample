package rules

import com.everis.drools.entity.*

// Sumamos el importe total de los productos
rule "Initial rule"
	salience 20
	when
		order : Order();
		totalPrice : Double() from accumulate (
			Product( productPrice : price) from order.getProducts,
			init (double total = 0;),
			action (total += productPrice;),
			result (new Double(total))
		);
	then
		order.setTotalPrice(totalPrice);
		//System.out.println("Total price is: " + totalPrice);
end

// Comprobamos si el cliente es SILVER, si es así aplicamos un 5% de descuento
rule "SILVER customer rule"
	salience 15
	when
		order : Order();
		customer : Customer ( status == Customer.SILVER_CUSTOMER) from order.getCustomer();
	then
		order.setTotalPrice(order.getTotalPrice() * ( 1 - (5 / 100d)));
end

// Comprobamos si el cliente es GOLD, si es así aplicamos un 10% de descuento
rule "GOLD customer rule"
	salience 15
	when
		order : Order();
		customer : Customer ( status == Customer.GOLD_CUSTOMER) from order.getCustomer();
	then
		order.setTotalPrice(order.getTotalPrice() * ( 1 - (10 / 100d)));
end

// Comprobamos si el pedido tiene 10 o más productos, si es así aplicamos un 15% de descuento
// Esta regla solo será aplicada entre Julio y Octubre de 2018
rule "Number of products rule"	
	salience 10	
	date-effective "01-JUL-2018"
	date-expires "01-OCT-2018"
	when
        order : Order (products.size() >= 10);
    then
    	order.setTotalPrice(order.getTotalPrice() * (1 - (15 / 100d) ) );
end
